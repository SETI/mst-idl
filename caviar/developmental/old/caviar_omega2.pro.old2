function caviar_omega2, r, domega_dr, domega2_dr=domega2_dr, lc82=lc82, gm=_gm, prad=_prad, j2=_j2, j4=_j4, j6=_j6, j8=_j8

if n_params() eq 0 then begin
  print, 'Syntax:  Result = CAVIAR_OMEGA2( r, domega_dr )'
  print, 'Returns the square of the mean motion (radians/sec) for radius r in Saturn''s ring plane,'
  print, 'accounting for Saturn''s j2, j4, and j6.  '
  print, 'Optionally returns the derivative (radians/sec/km) as well.'
  retall
endif

;; Saturn mass, radius, and harmonics from JPL kernel cpck05May2004.tpc
;gm = 37931289.4836 ;km^3/s^2
;prad = 60330.0d0 ;km
;j2 = 0.016292243237d
;j4 = -0.000928716077d
;j6 = 0.000088845313
saturn_constants, gm=gm, prad=prad, j2=j2, j4=j4, j6=j6;, j8=j8
if not keyword_set(j8) then j8=0
if keyword_set(lc82) then begin
  ; Use values from Lissauer and Cuzzi (1982)
  gm = 37929141.6d ;km^3/s^2
  j2 = 16299.1d-6
  j4 = -916.7d-6
  j6 = 81.3d-6
  j8 = 0d
endif
; Overwrite with input values, if they exist
if keyword_exists(_gm) then gm = _gm
if keyword_exists(_prad) then prad = _prad
if keyword_exists(_j2) then j2 = _j2
if keyword_exists(_j4) then j4 = _j4
if keyword_exists(_j6) then j6 = _j6
if keyword_exists(_j8) then j8 = _j8

; omega^2 = gm/r^3 * ( 1 - Sum_i (i+1)*P_i(0)*J_i*(prad/r)^i )
omega2 = gm / r^3 * ( 1 + j2*3/2*(prad/r)^2 - j4*15/8*(prad/r)^4 + $
                          j6*35/16*(prad/r)^6 - j8*315/128*(prad/r)^8 )
; d(omega^2)/dr = 3*gm/r^4 * ( -1 + Sum_i (i+1)*(i+3)/3*P_i(0)*J_i*(prad/r)^i )
domega2_dr = 3 * gm / r^4 * ( -1 - j2*5/2*(prad/r)^2 + j4*35/8*(prad/r)^4 - $
                                   j6*105/16*(prad/r)^6 + $
                                   j8*1155/128*(prad/r)^8 )
; d(omega^2)/dr = 2*omega*d(omega)/dr
domega_dr = domega2_dr / 2 / sqrt(omega2)

return, omega2

end
