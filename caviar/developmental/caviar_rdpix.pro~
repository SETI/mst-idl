; $Id: rdpix.pro,v 1.13 2003/02/03 18:13:24 scottm Exp $
;
; Copyright (c) 1989-2003, Research Systems, Inc.  All rights reserved.
;       Unauthorized reproduction prohibited.

Pro caviar_rdpix,Image,cam_params,cmat,nl,et,polera,poledec,sc,X0,Y0,x=x,y=yy,q=q,radec=radec,cray=cray
;Read the value of the pixel under the cursor
;Display x,y and the pixel value under the cursor
;+
; NAME:
;	RDPIX
;
; PURPOSE:
;	Interactively display the X position, Y position, and pixel value
;	of the cursor.
;
; CATEGORY:
;	Image display.
;
; CALLING SEQUENCE:
;	RDPIX, Image [, X0, Y0]
;
; INPUTS:
;	Image:	The array that represents the image being displayed.  This
;		array may be of any type.  Rather reading pixel values from
;		the display, they are taken from this parameter, avoiding
;		scaling difficulties.
;
; OPTIONAL INPUT PARAMETERS:
;	X0, Y0:	The location of the lower-left corner of the image area on
;		screen.  If these parameters are not supplied, they are
;		assumed to be zero.
;
; OUTPUTS:
;	None.
;
; COMMON BLOCKS:
;	None.
;
; SIDE EFFECTS:
;	The X, Y, and value of the pixel under the cursor are continuously
;	displayed.
;
; RESTRICTIONS:
;	None.
;
; PROCEDURE:
;	Instructions are printed and the pixel values are printed as the
;	cursor is moved over the image.
;
;	Press the left or center mouse button to create a new line of output,
;	saving the previous line.
;
;	Press the right mouse button to exit the procedure.
;
; MODIFICATION HISTORY:
;	DMS, Dec, 1987.
;	Rob Montgomery (rob@hao.ucar.edu), 9/21/92;
;		Correct indices for case of !order = 1
;
;-

COMPILE_OPT strictarr
on_error,2              ;Return to caller if an error occurs
print,'Press left or center mouse button for new output line.'
print,'... right mouse button to exit.'
s = size(image)
if s[0] ne 2 then message, 'Image parameter not 2d.'
s[1] = s[1]-1		;To n-1
s[2] = s[2]-1
!mouse.button=0
if n_elements(x0) le 0 then x0 = 0l
if n_elements(y0) le 0 then y0 = 1023l;not 0l because of Voyager convention
if s[s[0]+1] ge 4 then vform = 'F' else vform = 'I'
case !version.os_family of
        'Windows': cr = string("15b)+string("12b)	; carriage and new line
        'MacOS': cr = string("15b)			; carriage return
        'unix': cr = string("15b)			; carriage (for BC on
							; UNIX use CR rather
							; than CR/LF)
        else: cr = string("15b)				; carriage return"
endcase
formradec="($,'RA =',d13.8,' dec =',d13.8,' line =',i4,' sample =',i4,' DN =',"+vform+",a)";modified line,sample,DN
formradlon="($,'Radius =',d11.2,' Longitude =',d9.4,' line =',i4,' sample =',i4,' DN =',"+vform+",a)";modified line,sample,DN
form="($,'line =',i4,', sample =',i4,', DN =',"+vform+",a)"
;form="($,'x=',i4,', y=',i4,', value=',"+vform+",a)"
if keyword_set(cray) then cray=1 else cray=0
while !mouse.button ne 4 do begin
	CURSOR,x,y,2+cray,/dev
	x = x-x0 & y = y0 - y;modified to match usual line,sample convention for Voyager/Cassini images
	;x = x-x0 & y = y - y0
	if (!mouse.button and 3) ne 0 then begin	;New line?
	   p2radec,cam_params,cmat,nl,y,x,RA,dec
	   p2ralon,cmat,et,polera,poledec,sc,RA,dec,rad,lon
           if keyword_set(radec) then begin
	     print,form=formradec, ra, dec, y, x, Image[x,yy], string("12b)
           endif else begin
	     print,form=formradlon, rad, lon, y, x, Image[x,yy], string("12b)
           endelse
	   ;print,form="($,a)",string("12b)
	   while (!mouse.button ne 0) do begin wait,.1 & CURSOR,x,y,0,/dev & end
        endif
        if !mouse.button eq 16 then begin
          q = 1
          !mouse.button = 4
        endif
	if (x le s[1]) and (y le s[2]) and (x ge 0) and (y ge 0) then begin
	   if (!order eq 1) then yy = s[2] - y else yy = y
	   print,form = form, y,x,Image[x,yy],cr
	endif
endwhile
print,form="(/)"
end
